
---------------------------------------------------------------------------------------------------
-- Functions                                                                                     --
---------------------------------------------------------------------------------------------------

define id          x := x
define compose g f x := g (f x)
define const     a x := a

define let e f := f e

define $ := compose

define combY := \f. (\x. f (x x)) (\x. f (x x))
define combZ := \f. (\x. f (\v. x x v)) (\x. f (\v. x x v))




---------------------------------------------------------------------------------------------------
-- Unit Type                                                                                     --
---------------------------------------------------------------------------------------------------

define unit     := \u. u

define recU a u := u a

define matchU u f := f u




---------------------------------------------------------------------------------------------------
-- Booleans                                                                                      --
---------------------------------------------------------------------------------------------------

define true  a b := a
define false a b := b

define recB x y b := b x y

define matchB b t f := recB (t true) (f false) b

define if      b t f := recB t f b
define not     x a b := x b a
define or      x y := x true y
define and     x y := x y false
define xor     x y := x (not y) y
define implies x y := x y true

define showBool b := \true false bool. if b true false




---------------------------------------------------------------------------------------------------
-- Pairs                                                                                         --
---------------------------------------------------------------------------------------------------

define pair  a b := \s. s a b

define fst    p := p true
define snd    p := p false

define recP f p := f (fst p) (snd p)

define matchP p f := recP (\a b. f unit a unit b unit) p

define swap    p := pair (snd p) (fst p)
define apFst f p := pair (f (fst p)) (snd p)
define apSnd f p := pair (fst p) (f (snd p))
define ap  f g p := matchP p (\{ a , b }. pair (f a) (g b))

define showPair p := \{ , } pair. { (fst p) , (snd p) }




---------------------------------------------------------------------------------------------------
-- Sums                                                                                          --
---------------------------------------------------------------------------------------------------

define left  a := pair true a
define right b := pair false b

define recS f g s := recP (\z v. if z (f v) (g v)) s

define matchS s l r := recS (l unit) (r unit) s

define case    s f g := recS f g s
define isLeft  s     := case s (\z. true) (\z. false)
define isRight s     := case s (\z. false) (\z. true)

define showSum s := 
    \left right sum. 
        matchS s 
            (\left!  a. left a) 
            (\right! b. right b)




---------------------------------------------------------------------------------------------------
-- Natural Numbers                                                                               --
---------------------------------------------------------------------------------------------------

define 0      := \x f. x
define succ n := \x f. f (n x f)

define recN x f n := n x f

-- general recursion : c -> (n -> c -> c) -> n -> c
define genRecN c g n := recN (\k. c) (\s k. g k (s (succ k))) n 0

define matchN n x f := genRecN (x unit) (f unit) n

define isZero := recN true (\u. false)
define isEven := recN true not
define isOdd  := recN false not

define pred  n := \x f. recN (\u. x) (\g h. h (g f)) n id
define pred2 n := \x f. snd (recN (left x) (\s. case s (\z. right z) (\z. right (f z))) n)

define iterate f n s := recN s f n

define 1 := succ 0
define 2 := succ 1
define 3 := succ 2
define 4 := succ 3
define 5 := succ 4
define 6 := succ 5
define 7 := succ 6
define 8 := succ 7
define 9 := succ 8

define zero  := 0
define one   := 1
define two   := 2
define three := 3
define four  := 4
define five  := 5
define six   := 6
define seven := 7
define eight := 8
define nine  := 9

define + n m := recN m succ n
define - n m := recN n pred m
define * n m := recN 0 (+ m) n
define ^ n m := recN 1 (* n) m

define eqN n m := 
    if (isZero n) 
        (isZero m)
        (if (isZero m)
            false
            (recS
                (\k. false)
                (\b. b)
                (recN
                    (left m)
                    (\s. case s
                        (\k. let (pred k) (\k'. if (isZero k') (right true) 
                                    (left k')))

                        (\b. right false))
                    n)))

define fib  n := fst (recN (pair 0 1) (\p. apSnd (+ (snd p)) (swap p)) n)
define fact n := fst (recN (pair 1 1) (\p. ap (* (snd p)) succ p) n)
define tri  n := genRecN 0 (\k l. + (succ k) l) n

define div2 n   := recN (\b. 0) (\f b. if b (succ (f (not b))) (f (not b))) n false

define / n m := 
    let (pred m) \m'. 
    recN (\k. 0) (\f k. if (isZero k) (succ (f m')) (f (pred k))) n m'

define div := /

define showNatBin digits n := 
    \0 1 bin. snd 
        (recN
            (pair n (if (isEven n) 
                        (\s. s 0) 
                        (\s. s 1))) 
            (\p. recP 
                (\k v. 
                    (\h. pair h (if (isEven h) 
                                    (\s. v (\a. s 0 a))
                                    (\s. v (\a. s 1 a))))
                    (div2 k))
                p)
            (pred digits))
        id




---------------------------------------------------------------------------------------------------
-- Lists                                                                                         --
---------------------------------------------------------------------------------------------------

define empty    := \g e. e
define cons a l := \g e. g a (l g e)

define singleton a := cons a empty

define list n := 
    (recN 
        (\s. s (\l. l)) 
        (\f s. f (\g c. s (\l. g (cons c l)))) 
        n) 
    (\f. f empty)

define [] := empty
define @  := cons
define {} := singleton

define recL  e g l := l g e
define foldr e g l := l g e

define isEmpty l := recL true (\a b. false) l
define head    l := recL unit (\a xs. a) l
define tail    l := snd (recL (pair id empty) (\a p. pair (cons a) (recP (\f l. f l) p)) l)
define last    l := recL id (\a f. compose f (const a)) l unit
define init    l := recL (\z h. empty) (\a f z h. h z (f a cons)) l unit false

define map    f   := recL empty (\a bs. cons (f a) bs)
define concat u v := recL v cons u
define join   ls  := recL empty concat ls

define ++ := concat

define prepend x l := concat l (singleton x)

define listNatAsc n := recN (singleton 0) (\xs. cons 0 (map succ xs)) n
define listNatDsc n := recN (singleton 0) (\xs. cons (succ (head xs)) xs) n

define listNatAscBad n := genRecN (singleton n) (\k l. cons (pred (head l)) l) n
define listNatDscBad n := genRecN (singleton 0) (\k l. cons (succ k) l) n

define showList l := 
    \[ , ] list.
        if (isEmpty l) 
            ([ ])
            ((recL 
                (\s. s ])
                (\a f s. f (s , a))
                l)
            (\s. [))




---------------------------------------------------------------------------------------------------
-- Algebraic Lists                                                                               --
---------------------------------------------------------------------------------------------------

define emptyA    := left unit
define consA a l := right (pair a l)

define headA l := case l (\u. emptyA) fst
define tailA l := case l (\u. emptyA) snd

-- Is this derivable for a general algebraic type?
define recAL_kappa := \a l u g. u a (g l u g) u g
define recAL_eta   := \x l u g. case l (\z. x) (\z. z recAL_kappa u g)
define recAL x f l := recAL_eta x l (\a l u g. f a l) (recAL_eta x)

define algListToList l  := recAL empty cons l
define listToAlgList al := recL emptyA consA al

define isEmptyA l := recAL true (\a b. false) l
define headA    l := recAL unit (\a xs. a) l
define tailA    l := snd (recAL (pair id emptyA) (\a p. pair (consA a) (recP (\f l. f l) p)) l)

define showAlgList al := showList (algListToList al)




---------------------------------------------------------------------------------------------------
-- Vectors                                                                                       --
---------------------------------------------------------------------------------------------------

-- vector constructor, takes n arguments
define vector n := recN (\g s. g (s n)) (\f g e. f (\x. g (x e))) n id

define extend v e := \s. v (\k. s (succ k) e)

define dim v := v (\k. if (isZero k) 0 (recN (\x. k) (\f x. f) (pred k)))

define coord i v := 
    let (recP
            (\rest f. recN f (\g _. g) rest)
            (recN 
                (pair (dim v) (\a. a)) 
                (\p. recP (\sz f. pair (pred sz) (\a _. f a)) p) (pred i)))
    \accessor.
        v accessor

define mapV f v :=
    recN
        (\g s. v (\n. g (s n)))
        (\h g s. h (\k x. g (k (f x))) s)
        (dim v)
    id


define showVector v :=
    \< , > vector. 
        if (isZero (dim v))
            (< >)
            ((recL 
                (\s. s >) 
                (\a f s. f (s , a)) 
                (map (\i. coord i v) (tail (listNatAsc (dim v))))) 
            (\s. <))




---------------------------------------------------------------------------------------------------
-- Trees                                                                                         --
---------------------------------------------------------------------------------------------------

define leaf   a   := \f g. f a
define branch b l := \f g. g b (map (\t. t f g) l)

define recT f g t := t f g

define showTree t :=
    \[ , ] list leaf branch.
        recT (\a. leaf a) (\b l. branch b (showList l [ , ] list)) t




---------------------------------------------------------------------------------------------------
-- BTrees                                                                                        --
---------------------------------------------------------------------------------------------------

define leafB   a       := \f g. f a
define branchB b t0 t1 := \f g. g b (t0 f g) (t1 f g)

define recBT f g t := t f g

define showBTree t :=
    \leaf branch.
        recBT (\a. leaf a) (\b t0 t1. branch b t0 t1) t
